import: zeromq("net/zeromq")
import: json("fmt/json")
import: glib("gir/GLib")
import: gtk("gir/Gtk@3.0")
import: hmac("digest/hmac")
import: base16("enc/base16")

let Config := with Json := file(sys::Args[1], "r") do
	must Json:close
	json::decode(Json:rest)
end

print('\n\nConfig = {Config}\n')

let Key := Config["key"]
let Session := string(util::uuid())
let Compiler := compiler(global)
let Parser := parser()
var Counter := 0

print('Starting session = {Session}\n')

let Heartbeat := zeromq::socket(zeromq::socket::type::Rep)
Heartbeat:bind('{Config["transport"]}://{Config["ip"]}:{Config["hb_port"]}')

let Shell := zeromq::socket(zeromq::socket::type::Router)
Shell:bind('{Config["transport"]}://{Config["ip"]}:{Config["shell_port"]}')

let Control := zeromq::socket(zeromq::socket::type::Router)
Control:bind('{Config["transport"]}://{Config["ip"]}:{Config["control_port"]}')

let Stdin := zeromq::socket(zeromq::socket::type::Router)
Stdin:bind('{Config["transport"]}://{Config["ip"]}:{Config["stdin_port"]}')

let IOPub := zeromq::socket(zeromq::socket::type::Pub)
IOPub:bind('{Config["transport"]}://{Config["ip"]}:{Config["iopub_port"]}')

def Sockets := {}

fun check_socket(Name, Socket, Callback) do
	if Socket:events /\ 1 = 1 then
		let Message := list(zeromq::message(Socket), :data)
		print('{Name} <- {Message}\n')
		let Ids := if let Index := Message:find("<IDS|MSG>") then
			Message:splice(1, Index)
		else
			[]
		end
		let Signature := Message:pop
		let Header := json::decode(Message:pop or "null")
		let Parent := json::decode(Message:pop or "null")
		let Metadata := json::decode(Message:pop or "null")
		let Content := json::decode(Message:pop or "null")
		Callback(Ids, Signature, Header, Parent, Metadata, Content)
	end
end

fun check_sockets() do
	for Name, (Socket, Callback) in Sockets do
		check_socket(Name, Socket, Callback)
	end
end

fun listen(Name, Socket, Callback) do
	Sockets[Name] := (Socket, Callback)
	glib::unix_fd_add_full(0, Socket:fd, glib::IOCondition::in; Fd, Condition) do
		check_socket(Name, Socket, Callback)
		ret true
	on Error do
		print('Error: {Error:type}: {Error:message}\n{Error:trace}\n')
		ret true
	end
end

fun send(Name, Socket, Ids, MsgType, Parent, Metadata, Content, [Extra]) do
	let MsgId := string(util::uuid())
	let HeaderJson := json::encode({
		"msg_id" is MsgId,
		"username" is "kernel",
		"session" is Session,
		"msg_type" is MsgType,
		"version" is "5.3",
		"date" is string(time(), nil):limit(27) + "Z"
	})
	let ParentJson := json::encode(Parent)
	let MetadataJson := json::encode(Metadata)
	let ContentJson := json::encode(Content)
	let HMAC := hmac::sha256(Key)
	HMAC:update(HeaderJson)
	HMAC:update(ParentJson)
	HMAC:update(MetadataJson)
	HMAC:update(ContentJson)
	let Signature := base16::encode(HMAC:digest):lower
	let Message := list(Ids):put(Signature, HeaderJson, ParentJson, MetadataJson, ContentJson):grow(Extra)
	print('{Name} -> {Message}\n')
	zeromq::message(Message):send(Socket)
	check_sockets()
	ret MsgId
end

class: iopub_stream(io::stream, :socket, :name, :parent)

meth :write(Stream: iopub_stream, Bytes: address) do
	send("IOPub", Stream:socket, ["<IDS|MSG>"], "stream", Stream:parent, {}, {
		"name" is Stream:name,
		"text" is string(Bytes)
	})
	ret Bytes:size
end

let Stdout := iopub_stream(IOPub, "stdout")
let Stderr := iopub_stream(IOPub, "stderr")

Compiler:def("print", Stdout:write(_))

listen("Heartbeat", Heartbeat; Ids, Signature, Header, Parent, Metadata, Content) do
end

method: image()

meth image(V: any) := string
meth image(V: nil) "nil"
meth image(S: string) '\"{S:escape}\"'
meth image(A: address) with B := string::buffer() do
	B:write('<{A:size}:')
	for I in 0 .. (A:size - 1) do
		B:write(string((A + I):getu8, "%02X"))
	end
	B:write(">")
	B:rest
end
meth image(A: array) := string
meth image(L: list) '[{list(L, image), ", "}]'
meth image(M: map) with B := string::buffer() do
	def some := not nil
	B:write("{")
	if M:count > 0 then
		let (K, V) := first2(M)
		B:write(image(K))
		if V != some then
			B:write(" is ", image(V))
		end
		for K, V in M skip 1 do
			B:write(", ", image(K))
			if V != some then
				B:write(" is ", image(V))
			end
		end
	end
	B:write("}")
	B:rest
end
meth image(E: error) 'error(\"{E:type}\", \"{E:message}\")'

method: display()

meth display(D: map, V: any) D:insert("text/plain", string(V))
meth display(D: map, V: nil) D:insert("text/plain", "nil")
meth display(D: map, S: string) '\"{S:escape}\"'
meth display(D: map, A: address) with B := string::buffer() do
	B:write('<{A:size}:')
	for I in 0 .. (A:size - 1) do
		B:write(string((A + I):getu8, "%02X"))
	end
	B:write(">")
	D:insert("text/plain", B:rest)
end
meth display(D: map, A: array) D:insert("text/plain", string(A))
meth display(D: map, A: vector) do
	D:insert("text/plain", string(A))
	let C := A:shape[1]
	with Buffer := string::buffer() do
		Buffer:write("\\begin{pmatrix}\n")
		Buffer:write(A[1])
		for J in 2 .. C do
			Buffer:write("&", A[J])
		end
		Buffer:write("\n\\end{pmatrix}")
		D:insert("text/latex", Buffer:rest)
	end
	with Buffer := string::buffer() do
		Buffer:write("<table>")
		Buffer:write("<tr>")
		for J in 1 .. C do
			Buffer:write('<td>{A[J]}</td>')
		end
		Buffer:write("</tr>")
		Buffer:write("</table>")
		D:insert("text/html", Buffer:rest)
	end
end
meth display(D: map, A: matrix) do
	D:insert("text/plain", string(A))
	let (R, C) := A:shape
	with Buffer := string::buffer() do
		Buffer:write("\\begin{pmatrix}\n")
		for I in 1 ..< R do
			Buffer:write(A[I, 1])
			for J in 2 .. C do
				Buffer:write("&", A[I, J])
			end
			Buffer:write("\\\\\n")
		end
		Buffer:write(A[R, 1])
		for J in 2 .. C do
			Buffer:write("&", A[R, J])
		end
		Buffer:write("\n\\end{pmatrix}")
		D:insert("text/latex", Buffer:rest)
	end
	with Buffer := string::buffer() do
		Buffer:write("<table>")
		for I in 1 .. R do
			Buffer:write("<tr>")
			for J in 1 .. C do
				Buffer:write('<td>{A[I, J]}</td>')
			end
			Buffer:write("</tr>")
		end
		Buffer:write("</table>")
		D:insert("text/html", Buffer:rest)
	end
end
meth display(D: map, L: list) D:insert("text/plain", '[{list(L, image), ", "}]')
meth display(D: map, M: map) with B := string::buffer() do
	def some := not nil
	B:write("{")
	if M:count > 0 then
		let (K, V) := first2(M)
		B:write(image(K))
		if V != some then
			B:write(" is ", image(V))
		end
		for K, V in M skip 1 do
			B:write(", ", image(K))
			if V != some then
				B:write(" is ", image(V))
			end
		end
	end
	B:write("}")
	D:insert("text/plain", B:rest)
end
meth display(D: map, E: error) D:insert("text/plain", 'error(\"{E:type}\", \"{E:message}\")')

let DebugLog := file("/tmp/minikernel.log", "w+")

listen("Shell", Shell; Ids, Signature, Header, Parent, Metadata, Content) do
	switch Header["msg_type"]: string
	case "kernel_info_request" do
		send("Shell", Shell, Ids, "kernel_info_reply", Header, {}, {
			"status" is "ok",
			"protocol_version" is "5.3.0",
			"implementation" is "minilang",
			"implementation_version" is "0.0.1",
			"language_info" is {
				"name" is "minilang",
				"version" is "0.0.1",
				"mimetype" is "text/x-minilang",
				"file_extension" is ".mini",
				"codemirror_mode" is "minilang"
			},
			"banner" is "Minilang in Jupyter :)"
		});
		send("IOPub", IOPub, ["<IDS|MSG>"], "status", Header, {}, {"execution_state" is "idle"})
	case "execute_request" do
		Content["store_history"] = true and Counter:inc
		Compiler:def("print"; [Args]) do
			send("IOPub", IOPub, ["<IDS|MSG>"], "stream", Header, {}, {
				"name" is "stdout",
				"text" is Args -> string join ""
			})
			ret nil
		end
		Parser:source('Cell<{Counter}>', 0)
		Parser:reset:input(Content["code"])
		Stdout:parent := Header
		Stderr:parent := Header
		send("IOPub", IOPub, ["<IDS|MSG>"], "status", Header, {}, {"execution_state" is "busy"})
		let Result := {"execution_count" is Counter}
		do
			loop
				let Result := Parser:evaluate(Compiler)
				until type(Result) = any and Result = compiler::EOI
				let Data := {}
				display(Data, Result)
				send("IOPub", IOPub, ["<IDS|MSG>"], "execute_result", Header, {}, {
					"execution_count" is Counter,
					"data" is Data,
					"metadata" is {}
				})
			end
			Result["status"] := "ok"
			Result["payload"] := []
			Result["user_expressions"] := {}
			Result["user_variables"] := {}
		on Error do
			Stderr:write('{Error:type}: {Error:message}\n\t{Error:trace -> _:join(":") join("\n\t")}\n')
			Result["status"] := "error"
			Result["ename"] := Error:type
			Result["evalue"] := Error:message
			Result["traceback"] := list(Error:trace, _:join(":"))
		end
		send("Shell", Shell, Ids, "execute_reply", Header, {}, Result)
		send("IOPub", IOPub, ["<IDS|MSG>"], "status", Header, {}, {"execution_state" is "idle"})
	else
		print('Unknown message type = {Header["msg_type"]}\n')
	end
end

listen("Control", Control; Ids, Signature, Header, Parent, Metadata, Content) do
	switch Header["msg_type"]: string
	case "kernel_info_request" do
		send("Control", Control, Ids, "kernel_info_reply", Header, {}, {
			"status" is "ok",
			"protocol_version" is "5.3.0",
			"implementation" is "minilang",
			"implementation_version" is "0.0.1",
			"language_info" is {
				"name" is "minilang",
				"version" is "0.0.1",
				"mimetype" is "text/x-minilang",
				"file_extension" is ".mini",
				"codemirror_mode" is "minilang"
			},
			"banner" is "Minilang in Jupyter :)"
		});
		send("IOPub", IOPub, ["<IDS|MSG>"], "status", Header, {}, {"execution_state" is "idle"})
	case "shutdown_request" do
		halt()
	else
		print('Unknown message type = {Header["msg_type"]}\n')
	end
end

listen("Stdin", Stdin; Message) do
end

